from ymp.util import glob_wildcards
from ymp.config import icfg

rule megahit:
    """
    Megahit assembly rule. Supports reads.by_COLUMN.mhc/complete as target for
    by group co-assembly.
    """
    message:
        "(Co-)Assembling {wildcards.target} with megahit"
    input:
        r1 = "{dir}/{: sources :}.{: pairnames[0] :}.fq.gz",
        r2 = "{dir}/{: sources :}.{: pairnames[1] :}.fq.gz"
    output:
        fasta = "{dir}{by}.mhc/{target}.contigs.fasta.gz",
        fastg = "{dir}{by}.mhc/{target}.contigs.fastg.gz"
    log:
        "{dir}{by}.mhc/{target}.log.gz"
    params:
        workdir = "{dir}{by}.mhc/{target}/",
        tmpdir  = icfg.expand("{:dir.tmp:}"),
        preset  = "meta-sensitive",
        r1 = lambda wc, input: ",".join([input.r1]
                                        if isinstance(input.r1, str) else input.r1),
        r2 = lambda wc, input: ",".join([input.r2]
                                        if isinstance(input.r2, str) else input.r2)
    threads:
        16
    conda:
        srcdir("megahit.yml")
    shell: """
    # delete workdir if no opts.txt was recorded (never started really)
    [ -e {params.workdir}/opts.txt ] || rm -rf {params.workdir}

    megahit -1 {params.r1} -2 {params.r2} \
      --presets {params.preset} \
      --num-cpu-threads {threads} \
      --out-dir {params.workdir} \
      --tmp-dir {params.tmpdir} \

    # quit if not finished
    [ -e {params.workdir}/done ] || exit 1

    # output zipped contigs
    pigz -p {threads} -9 -c {params.workdir}/final.contigs.fa > {output.fasta}

    # output the zipped log
    cat {params.workdir}/{{opts.txt,log}} |\
    pigz -p {threads} -9 -c > {log}

    # determine largest K used
    MAXK=`tac {params.workdir}/log | sed -nr 's/^---.*k = ([0-9]{{2,3}}) ---/\\1/p;' | head -n1`

    # output the zipped fastg
    megahit_toolkit contig2fastg $MAXK \
    {params.workdir}/intermediate_contigs/k${{MAXK}}.contigs.fa |\
    pigz -p {threads} -9 -c > {output.fastg}

    # remove intermediate contigs
    #rm -rf {params.workdir}
    """


localrules: metaspades_input_yaml
rule metaspades_input_yaml:
    """
    Prepares a dataset config for spades. Spades commandline is limited to
    at most 9 pairs of fq files, so to allow arbitrary numbers we need to
    use the dataset config option.

    Preparing in a separate rule so that the main metaspades rule can use
    the `shell:` rule and not `run:`, which would preclude it from
    using conda environments.
    """
    message:
        "Preparing Config YAML for MetaSpades"
    input:
        r1 = "{dir}/{: sources :}.{: pairnames[0] :}.fq.gz",
        r2 = "{dir}/{: sources :}.{: pairnames[1] :}.fq.gz"
    output:
        yaml = "{dir}{by}.sp/{target}.yaml"
    run:
        import yaml
        from ymp.util import filter_out_empty

        r1, r2 = filter_out_empty(input.r1, input.r2)

        with open(output.yaml, "w") as cfg:
            cfg.write(yaml.safe_dump([{
                "left reads": r1,
                "right reads": r2,
                "type": "paired-end",
                "orientation": "fr"
            }]))


rule metaspades:
    """
    Runs MetaSpades. Supports reads.by_COLUMN.sp/complete as target for
    by group co-assembly.
    """
    message:
        "(Co-)Assembling {wildcards.target} with MetaSpades"
    input:
        conf = "{dir}{by}.sp/{target}.yaml"
    output:
        fasta = "{dir}{by}.sp/{target}.contigs.fasta.gz",
        fastg = "{dir}{by}.sp/{target}.contigs.fastg.gz"
    log:
        "{dir}{by}.sp/{target}.log.gz"
    params:
        workdir = "{dir}{by}.sp/{target}/",
        tmpdir  = "/scratch/$HOME/tmp",
        memory  = 500
    threads:
        16
    shell: """
    metaspades.py \
        -o {params.workdir} \
        --threads {threads} \
        --tmp-dir {params.tmpdir} \
        --dataset {input.conf} \
        --memory {params.memory} \
        --continue

    pigz -p {threads} -9 -c {params.workdir}/scaffolds.fasta > {output.fasta}
    pigz -p {threads} -9 -c {params.workdir}/assembly_graph.fastg > {output.fastg}
    cat {params.workdir}/{{params.txt,spades.log}} | pigz -p {threads} -9 > {log}
    # rm -rf {params.workdir}
    """


rule metaquast_all_at_once:
    message:
        "MetaQUAST qc'ing {wildcards.dir} combined co-assemblies"
    input:
        "{dir}/{:targets:}.contigs.fasta.gz"
    output:
        report="{dir}.mqc/report.tsv",
        dir="{dir}.mqc/"
    log:
        "{dir}.mqc/metaquast.log"
    params:
        targets = icfg.expand("{:targets:}"),
        min_contig_len = 500
    conda:
        srcdir("quast.yml")
    threads:
        16
    shell:"""
    TARGETS="{params.targets}"
    metaquast \
          -o {output.dir} \
          -t {threads} \
          -l ${{TARGETS// /,}} \
          --min-contig {params.min_contig_len} \
          {input}
    """


rule metaquast_part:
    message:
        "MetaQUAST qc'ing {wildcards.dir} assembly {wildcards.sample}"
    input:
        "{dir}/{sample}.contigs.fasta.gz"
    output:
        report="{dir}.mq/{sample}/combined_reference/report.tsv",
        dir="{dir}.mq/{sample}"
    log:
        "{dir}.mq/{sample}/metaquast.log"
    params:
        min_contig_len = 500
    conda:
        srcdir("quast.yml")
    threads:
        8
    shell:"""
    metaquast \
          -o {output.dir} \
          -t {threads} \
          -l {wildcards.sample} \
          --min-contig {params.min_contig_len} \
          {input}
    """


rule quast_report:
    message: "MultiQC aggregating QUAST reports"
    input:
        "{dir}.mq/{:targets:}/combined_reference/report.tsv"
    output:
        "reports/{dir}.mq.html"
    log:
        "{dir}.mq/multiqc.log"
    params:
        dir = "{dir}.mq"
    conda:
        srcdir("multiqc.yml")
    threads:
        1
    shell: """
    echo {input} | tr ' ' '\n' > {params.dir}/files.txt
    sed 's|/combined_reference/report.tsv||; s|.*/||' \
        {params.dir}/files.txt > {params.dir}/names.txt
    multiqc \
      --module quast \
      --force \
      --title {wildcards.dir} \
      --no-data-dir \
      --file-list {params.dir}/files.txt \
      --sample-names {params.dir}/names.txt \
      --filename {output} \
      --verbose > {log} 2>&1
    """
