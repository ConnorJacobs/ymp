rule bbmap:
    """Map read from each (co-)assembly read file to the assembly"""
    message:
        "BBMap mapping sample {wildcards.source} to assembly {wildcards.target}"
    input:
        contigs="{dir}{by}.mhc/{target}.contigs.fasta.gz",
        pairs="{dir}/{source}.{: pairnames :}.fq.gz"
    output:
        bam="{dir}{by}.mhc.bbm/{target}.contigs.{source}.bam",
        stats="{dir}{by}.mhc.bbm/{target}.contigs.{source}.stats",
        ihist="{dir}{by}.mhc.bbm/{target}.contigs.{source}.ihist"
    log:
        "{dir}{by}.mhc.bbm/{source}.contigs.{source}.log"
    threads:
        8
    conda:
        srcdir("bbmap.yml")
    shell: """
    bbmap.sh threads={threads} pigz unpigz \
             nodisk ref={input.contigs} \
             in={input.pairs[0]} in2={input.pairs[1]} \
             out={output.bam} \
             machineout statsfile={output.stats} \
             ihist={output.ihist} \
             ambiguous=all \
             mdtag \
             trimreaddescriptions \
             >{log} 2>&1
    """
# FIXME: document above options


rule bowtie_index:
    message:
        "Bowtie2: Indexing {input.contigs}"
    input:
        contigs="{dir}/{sample}.contigs.fasta"
    output:
        temp(expand("{{dir}}.bt2/{{sample}}.contigs.{ext}",
                    ext="1.bt2 2.bt2 3.bt2 4.bt2 rev.1.bt2 rev.2.bt2".split()))
    params:
        bt2_base="{dir}.bt2/{sample}.contigs"
    threads:
        8
    log:
        "{dir}/{sample}.contigs.btbuild.log"
    conda:
      srcdir("bowtie2.yml")
    shell:"""
    bowtie2-build-s \
      {input.contigs} \
      {params.bt2_base} \
      --threads {threads} \
      >& {log}
    """


rule bowtie_map_partial_coassembly:
    message:
        "Bowtie2: Mapping sample {wildcards.source} to assembly {wildcards.target}"
    input:
        contigs = "{dir}{by}.mhc/{target}.contigs.fasta.gz",
        index   = "{dir}{by}.mhc.bt2/{target}.contigs.1.bt2",
        pairs   = "{dir}/{source}.{: pairnames :}.fq.gz"
    output:
        bam     = "{dir}{by}.mhc.bt2/{target}.contigs.{source}.bam",
        stats   = "{dir}{by}.mhc.bt2/{target}.contigs.{source}.stats"
    log:
                  "{dir}{by}.mhc.bt2/{target}.contigs.{source}.log"
    params:
        bt2_base= "{dir}{by}.mhc/{target}.contigs",
        maxins = 800,
        k = 100
    threads:
        16
    conda:
      srcdir("bowtie2.yml")
    shell:"""
    bowtie2 \
        -x {params.bt2_base} \
        -1 {input.pairs[0]} -2 {input.pairs[1]} \
        -X {params.maxins} \
        --met-file {output.stats} \
        -k {params.k} \
        -p {threads} \
        2>{log} \
        | samtools view -b -o {output.bam} -
    """


rule samtools_sort:
    message:
        "Samtools: Sorting BAM file {input}"
    input:
        "{path}.bam"
    output:
        "{path}.sorted.bam__DISABLED"
    params:
        mem=32
    threads:
        8
    conda:
      srcdir("samtools.yml")
    shell:"""
    samtools sort -m {params.mem}G --threads {threads} -o {output} {input}
    """


rule samtools_index:
    message:
        "Samtools: Indexing BAM file {input}"
    input:
        "{path}.bam"
    output:
        "{path}.bam.bai__DISABLED"
    threads:
        1
    conda:
      srcdir("samtools.yml")
    shell:"""
    samtools index {input}
    """


rule sambamba_sort:
    message: "Sambamba: Sorting BAM file {input}"
    input:   "{path}.bam"
    output:  "{path}.sorted.bam",
             "{path}.sorted.bam.bai"
    log:     "{path}.sorted.bam.log"
    benchmark: "benchmarks/sambamba_sort/{path}.txt"
    params:  mem=32,
             compress=6
    threads: 8
    conda: srcdir("sambamba.yml")
    shell:
        "sambamba sort"
        " --memory-limit={params.mem}GB"
        " --compression-level={params.compress}"
        " --nthreads={threads}"
        " {input}"
        " >{log} 2>&1"


rule all_mapped:
    input:
        "{dir}/{target}.contigs.{:sources:}.sorted.bam.bai"
    output:
        "{dir}/complete_{target}"
    shell:
        "touch {output}"


rule complete_from_targets:
    output:
        "{dir}/complete"
    input:
        "{dir}/complete_{:targets:}"
    shell:
        "touch {output}"
