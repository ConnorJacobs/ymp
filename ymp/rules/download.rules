localrules: download_file
rule download_file:
    """
    Downloads remote file using *wget*
    """
    message:
        "Downloading {params.url}"
    output:
        "{:dir.downloads:}/{protocol}/{server}/{path}"
    log:
        "{:dir.downloads:}/{protocol}/{server}/{path}.log"
    wildcard_constraints:
        protocol="(http|https|ftp|ftps)",
        server="[^/]+"
    params:
        url="{protocol}://{server}/{path}"
    conda: "wget.yml"
    shell: """
    wget  "{params.url}" -O "{output[0]}" > {log} 2>&1
    """

ruleorder: download_file > gunzip
ruleorder: download_file > fq2fa


localrules: get_reference_file
rule get_reference_file:
    message: "Preparing {input}"
    input:   lambda wc: icfg.ref[wc.refname].get_file(wc.file),
             refdir = ancient(icfg.dir.references)
    output:  "{:dir.references:}/{refname}/{file}"
    threads: 1
    run:
        if wildcards.file.endswith(".gz"):
            if not input[0].endswith(".gz"):
                shell("gzip -c {input[0]} > {output}")
        else:
            os.symlink(
                os.path.relpath(
                    input[0],
                    os.path.dirname(output[0])
                ),
                output[0]
            )



localrules: get_reference_dir
rule get_reference_dir:
    message: "Installing {wildcards.refname} from {input.tar}"
    input:  tar = lambda wc: icfg.ref[wc.refname],
            refdir = ancient(icfg.dir.references)
    output: "{:dir.references:}/{refname}/",
    params:
        tempfolder = "{output}__ymp_temp"
    threads: 1
    shell: """
    rm -rf {params.tempfolder}
    mkdir -p {params.tempfolder}
    tar -xf {input.tar} -C {params.tempfolder}
    if test $(ls -1 {output}_temp | wc -l) -eq 1; then
      mv {output}_temp/* {output}
      rmdir {output}_temp
    else
      mv {output}_temp {output}
    fi
    """
ruleorder: get_reference_dir > gunzip
